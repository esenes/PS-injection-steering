!######################################################
!# For information only. Protons at 10 GeV/c. User: TSTPS
!#
!# Execute file with the command: >madx < 10GeVc_for_OP_group.madx
!#
!######################################################
!#
!#  IQF       =      0 A
!#  IQD       =      0 A


/******************************************************************
 * Circulating beam
 ******************************************************************/
call,  file = '../elements/PS.ele';
call,  file = '../sequence/PS.seq';
call,  file = '../strength/PS_10GeVc_for_OP_group.str';




/******************************************************************
 * Use
 ******************************************************************/
beam, particle=proton, pc=10; use, sequence=PS;

set,  format="-21s";
set,  format="12.8f";




/***************************************************
 * TWISS PTC
 ***************************************************/
ptc_create_universe;
ptc_create_layout,time=true,model=2,method=6,nst=5,exact; ! time = true => same as MADX, i.e. GammaTR = Q
ptc_twiss,table=ptc_twiss,closed_orbit,icase=5,no=3,summary_table; ! ,file="twiss";

select_ptc_normal, q1,q2; ! Write Q1 and Q2 in table "normal_results"
ptc_normal,normal,icase=2,no=2;
ptc_end;

!write,table=ptc_twiss;
!write, table=normal_results, file=normal_results;





/***************************************************
 * Write Injection_optics_Qx_<qx>_Q_<qy>.outx
 ***************************************************/
write_optics(qx,qy): macro={
   select,flag=ptc_twiss,clear;
   select,flag=ptc_twiss, column = name,angle,k1L,k2L,k3L,
                                   beta11,beta22,disp1,disp3,
                                   x,y,alfa11,alfa22,mu1,mu2,
                                   disp2,disp4,px,py;
   write,table=ptc_twiss,file="../for_OP_group/10GeVc_optics_Qx_0.qx_Qy_0.qy.outx";
};

Q1=100*table(normal_results,Q1,VALUE)+0.5;
Q2=100*table(normal_results,Q2,VALUE)+0.5;
exec, write_optics($Q1,$Q2);





/***************************************************
 * Write Injection_config
 ***************************************************/
write_config: macro={
   select,flag=ptc_twiss,clear;
   select,flag=ptc_twiss, column = KEYWORD, name, PARENT, L, S;
   write,table=ptc_twiss,file="../for_OP_group/10GeVc_config_file.outx";
};

exec, write_config();

system, "rm fort.18";
system, "rm end.map";




/******************************************************************
 * PLOT
 ******************************************************************/
resplot;
setplot, post=2;

PLOT, table=ptc_twiss, VAXIS=X,HAXIS=S,
      SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, FILE="../output/10GeVc";





/*******************************************************************************
 * survey
 *******************************************************************************/
 title, '!!! Survey initial values added for reference, but they should not be taken as official values !!!';
 theta0=-2.685360138347872905;
 sur1 : macro={
        use, sequence=PS;
        select, flag=survey,clear;
        select, flag=survey, column=name,s,l,z,x,y;
        survey, z0=2045.113039101714548451, x0=2008.589317718255415457, y0=2433.659999999999854481
              , theta0:=theta0
              , file="../output/PS.sur";
              }
 exec, sur1; ! NB: The result of the survey is in the file: "PS.sur"



STOP;
