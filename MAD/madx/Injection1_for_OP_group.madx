!######################################################
!# SFTPRO injection. Protons at 2.141766 GeV/c. Ekin=1.4 GeV. Users: TOF, EASTA, EASTC
!######################################################
!#
!#  INJECTION  (2.12 GeV/c, Qx=6.131, Qy=6.288 )
!#
!# Execute the file, with the comand: >madx < Injection1_for_OP_group.madx
!#
!######################################################



/******************************************************************************
 * INITIALIZATION
 ******************************************************************************/

TITLE, "Injection_optics with Qx=6.131, Qy=6.288";

call,  file = '../elements/PS.ele';
call,  file = '../sequence/PS.seq';
call,  file = '../strength/PS_Injection1_for_OP_group.str';





/******************************************************************************
 * BEAM and USE
 ******************************************************************************/

beam, particle=proton, pc=pc;
use, sequence=PS;

value, pc, beam->pc, beam->energy;


set,  format="-21s";
set,  format="12.8f";






/***************************************************
 * MATCHING for optics functions
 ***************************************************/
!option, -info;
!option, -echo;
!
!iqf =  -5.83607771 ; ! Result of matching: Qx=6.131, Qy=6.288
!iqd =  -2.88229612 ; ! Result of matching: Qx=6.131, Qy=6.288
!
! maketwiss : macro={
!                    use, sequence=PS;
!                    ptc_create_universe;
!                    ptc_create_layout,time=true,model=2,method=6,nst=5,exact;
!                    ptc_twiss, table=ptc_twiss,closed_orbit, icase=5, no=1;
!
!       //           select,flag=ptc_twiss,clear;
!       //           select,flag=ptc_twiss, column = name,s,mu1,mu2;
!       //           write,table=ptc_twiss,file="match.outx";
!
!                    value, table(ptc_twiss,ENDMARK,MU1),table(ptc_twiss,ENDMARK,MU2);
!                    value, IQF, IQD;
!                    ptc_end; ! WARNING:  The file 'ptc_twiss' is deleted by the ptc_end command
!                   }
!
!
! match, use_macro;
!          vary, name=IQF, step = 1.0E-9, lower=-10,  upper=10;
!          vary, name=IQD, step = 1.0E-9, lower=-10,  upper=10;
!          use_macro,name=maketwiss;
!
!          constraint, expr=table(ptc_twiss,ENDMARK,MU1)=6.131;
!          constraint, expr=table(ptc_twiss,ENDMARK,MU2)=6.288;
!
!          jacobian,  calls= 5, tolerance=1.0E-15, bisec=9;
!      //  simplex,   calls= 5, tolerance=1.0E-12;
!      //  lmdif,     calls=10, tolerance=1.0E-22;
!endmatch;
!
!value, iqf;
!value, iqd;



/***************************************************
 * TWISS PTC
 ***************************************************/
ptc_create_universe;
ptc_create_layout,time=true,model=2,method=6,nst=5,exact; ! time = true => same as MADX, i.e. GammaTR = Q
ptc_twiss,table=ptc_twiss,closed_orbit,icase=5,no=3,summary_table; ! ,file="twiss";

select_ptc_normal, q1,q2; ! Write Q1 and Q2 in table "normal_results"
ptc_normal,normal,icase=2,no=2;
ptc_end;

!write,table=ptc_twiss;
!write, table=normal_results, file=normal_results;



/********************* TWISS ***********************
select, flag=twiss, clear;
select, flag=twiss,column=name,angle,k1L,k2L,k3L,betx,bety,dx,dy,x,y,alfx,alfy,mux,muy,dpx,dpy,px,py;
twiss, DELTAP=0.000,  file="../for_OP_group/optics_ct_injection_slowbump.outx";
 ***************************************************/






/***************************************************
 * Write Injection_optics_Qx_<qx>_Q_<qy>.outx
 ***************************************************/
write_optics(qx,qy): macro={
   select,flag=ptc_twiss,clear;
   select,flag=ptc_twiss, column = name,angle,k1L,k2L,k3L,
                                   beta11,beta22,disp1,disp3,
                                   x,y,alfa11,alfa22,mu1,mu2,
                                   disp2,disp4,px,py;
   write,table=ptc_twiss,file="../for_OP_group/Injection1_optics_Qx_0.qx_Qy_0.qy.outx";
};

Q1=100*table(normal_results,Q1,VALUE)+0.5;
Q2=100*table(normal_results,Q2,VALUE)+0.5;
exec, write_optics($Q1,$Q2);







/***************************************************
 * Write Injection_config
 ***************************************************/
write_config: macro={
   select,flag=ptc_twiss,clear;
   select,flag=ptc_twiss, column = KEYWORD, name, PARENT, L, S;
   write,table=ptc_twiss,file="../for_OP_group/Injection1_config_file.outx";
};

exec, write_config();








system, "rm fort.18";
system, "rm end.map";

STOP;

