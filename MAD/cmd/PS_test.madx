
!######################################################
!# MADX-PTC files for non-linear lattice model at
!# different energies from 2008 measurements
!######################################################
!#
!#
!######################################################


 set,format="14.9f";


! Define macro for higher order chromaticity with ptc:
 ptcchrom2: macro={

   USE, sequence=PS;

   ptc_create_universe, SECTOR_NMUL_MAX=10,SECTOR_NMUL=10 ;
   ptc_create_layout,time=false,model=2,method=6,nst=3,exact;

   select,flag=ptc_twiss, clear;
   select,flag=ptc_twiss, column=name, s,x,px,beta11,beta22,alfa11,alfa22,mu1,mu2;
   ptc_twiss,closed_orbit,file=ps-ct-twiss.ptc;

   select_ptc_normal, q1=0, q2=0;
   select_ptc_normal,dq1=1,dq2=1;
   select_ptc_normal,dq1=2,dq2=2;
   select_ptc_normal,dq1=3,dq2=3;
   select_ptc_normal,dq1=4,dq2=4;
   ptc_align;
   ptc_normal,closed_orbit,normal,icase=5,no=5;
   ptc_end;

   qx0 =table(normal_results,value, 1);
   qx1 =table(normal_results,value, 3);
   qx2 =table(normal_results,value, 5);
   qx3 =table(normal_results,value, 7);
   qx4 =table(normal_results,value, 9);
   qy0 =table(normal_results,value, 2);
   qy1 =table(normal_results,value, 4);
   qy2 =table(normal_results,value, 6);
   qy3 =table(normal_results,value, 8);
   qy4 =table(normal_results,value,10);
 };



/******************************************************************
 * Circulating beam
 ******************************************************************/
 call,  file = '/afs/cern.ch/eng/ps/cps/Psring/models/2014/elements/PS.ele';
 call,  file = '/afs/cern.ch/eng/ps/cps/Psring/models/2014/sequence/PS.seq';
 call,  file = '/afs/cern.ch/eng/ps/cps/Psring/models/2014/strength/PS_AD_Extraction_NoExtractElem_for_OP_group.str';




 IQF := OFF ;
 IQD := OFF ;

 !========== 26 GeV/c (meas. 2008) ==========================
 Pc        := 26;
 Beam, particle = proton,pc=Pc, ex=0.75E-6, ey=0.5E-6, sige=0.0,NPART=2E10,BUNCHED;

 !------------ measured chromaticity -----------------
 !Qx=0.26483 +0.65349x -247.04x^2 +1508.2x^3 +1.3090e+5x^4
 !Qy=0.21745 +2.28070x -16.850x^2 +2749.9x^3 -5.1987e+5x^4
 MQx0:=0.26483;
 MQy0:=0.21745;
 MQx1:=0.65349;
 MQy1:=2.28070;
 MQx2:=-247.04;
 MQy2:=-16.850;
 MQx3:= 1508.2;
 MQy3:= 2749.9;
 MQx4:= 1.3090e+5;
 MQy4:=-5.1987e+5;
 pfki1f := -0.00273058769 ;
 pfki2f :=   0.0323854329 ;
 pfki3f :=   -0.476726763 ;
 pfki4f :=     6.07376399 ;
 pfki5f :=    -717.816686 ;
 pfki1d :=  0.00284286252 ;
 pfki2d :=  -0.0390095029 ;
 pfki3d :=    0.487527253 ;
 pfki4d :=    -10.1936398 ;
 pfki5d :=     2004.18362 ;


 !=================== some energy checks ====================
 MASS  := 0.93827202980;           ! [GeV/c^2]
 ENERGY:=sqrt(Pc*Pc +MASS*MASS);   ! [GeV/c^2]
 GAMMA :=ENERGY/MASS;              ! rel. gamma
 BETA  :=sqrt(1-1/(GAMMA*GAMMA));  ! rel. beta
 value, PC,ENERGY,GAMMA,BETA;


 !===================== TUNING ==============================
 !============== MATCHING WITH MACRO (PTC) ==================
 USE, sequence=PS;
 match,use_macro;
         vary,name=PFKI1F;
         vary,name=PFKI1D;
         vary,name=PFKI2F;
         vary,name=PFKI2D;
         vary,name=PFKI3F;
         vary,name=PFKI3D;
         vary,name=PFKI4F;
         vary,name=PFKI4D;
         vary,name=PFKI5F;
         vary,name=PFKI5D;
         use_macro,name=ptcchrom2;
         constraint,expr= qx0= 1*MQx0;
         constraint,expr= qy0= 1*MQy0;
         constraint,expr= qx1= 1*MQx1;
         constraint,expr= qy1= 1*MQy1;
         constraint,expr= qx2= 2*MQx2;
         constraint,expr= qy2= 2*MQy2;
         constraint,expr= qx3= 6*MQx3;
         constraint,expr= qy3= 6*MQy3;
         constraint,expr= qx4=24*MQx4;
         constraint,expr= qy4=24*MQy4;
 jacobian,calls=5,bisec=3;
 endmatch;

 value, PFKI1F,PFKI2F,PFKI3F,PFKI4F,PFKI5F;
 value, PFKI1D,PFKI2D,PFKI3D,PFKI4D,PFKI5D;

 value, MQx0,MQx1,MQx2,MQx3,MQx4;
 value, MQy0,MQy1,MQy2,MQy3,MQy4;

 value, qx0,qx1,qx2/2,qx3/6,qx4/24;
 value, qy0,qy1,qy2/2,qy3/6,qy4/24;

 value, pc;
 show,  beam;
 value, PC,ENERGY,GAMMA,BETA;


 stop;

